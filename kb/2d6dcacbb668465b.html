<html>
<head>
  <title>Напечатать миллиметровку</title>
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1251" />
  <link type="text/css" href="css/css.css" rel="stylesheet" />
  <link type="text/css" href="css/sh.css" rel="stylesheet" />
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <div id="logo"><img id="logo" src="img/logo.png" /></div>
  <div id="navigation">
    <p id="navigation">
      <a class="navigation" onclick="prev();" href="#">Предыдущая</a><br />
      <a class="navigation" onclick="up();" href="#">Наверх</a><br />
      <a class="navigation" onclick="next();" href="#">Следующая</a>
    </p>
  </div>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Напечатать миллиметровку</h1>
<div id="date">01.01.2010</div>
<!-- Actual content start -->

<pre name="code" class="delphi">
program PrnGrid;
 
uses
  Dialogs,
  Printers,
  Windows,
  Types;
 
{$R *.res}
 
function GetPenWidth(I: Integer): Integer;
begin
  if I mod 100 = 0 then
    Result := 45
  else if I mod 50 = 0 then
    Result := 30
  else if I mod 10 = 0 then
    Result := 20
  else if I mod 5 = 0 then
    Result := 15
  else
    Result := 10;
end;
 
var
  PageRect: TRect;
  I, J: Integer;
begin
  with TPrintDialog.Create(nil) do
  try
    if not Execute then
      Exit;
  finally
    Free;
  end;
 
  PageRect := Rect(0, 0, Printer.PageWidth, Printer.PageHeight);
  Printer.Title := 'Милиметровка';
  Printer.BeginDoc;
  try
    try
      with Printer.Canvas do
      begin
        SetMapMode(Handle, MM_HIMETRIC);
        DPtoLP(Handle, PageRect, 2);
 
        with PageRect do
        begin
         Inc(Left, 1000);
         Dec(Top, 1000);
         Dec(Right, 1000);
         Inc(Bottom, 1000);
        end;
 
        J := 0;
        I := PageRect.Left;
        while I &lt; PageRect.Right  do
        begin
          Pen.Width := GetPenWidth(J);
          MoveTo(I, PageRect.Top);
          LineTo(I, PageRect.Bottom);
          Inc(I, 100);
          Inc(J);
        end;
 
        J := 0;
        I := PageRect.Top;
        while I &gt; PageRect.Bottom do
        begin
          Pen.Width := GetPenWidth(J);
          MoveTo(PageRect.Left, I);
          LineTo(PageRect.Right, I);
          Dec(I, 100);
          Inc(J);
        end;
      end;
    except
      Printer.Abort;
      raise;
    end;
  finally
    Printer.EndDoc;
  end;
end.
</pre>

</div>
<!-- Actual content end -->
<hr />
<div id="footer">
<p>&copy; DRKB Library, 2010<br />Разработка и поддержка &mdash; <a href="http://www.drkb.ru/" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
