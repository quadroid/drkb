<!DOCTYPE html>
<html>
<head>
  <title>Как выделить файлы в Проводнике?</title>
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8"/>
  <link type="text/css" href="css/css.css" rel="stylesheet"/>
  <link type="text/css" href="css/sh.css" rel="stylesheet"/>
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <a title="DRKB Library" href="../splash.html"><div id="logo"></div></a>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Как выделить файлы в Проводнике?</h1>
<div id="date">01.01.2010</div>

<p>Просто открыть окно, и выделить в нём файлы|папки</p>
<p>Да в принципе путь то нам известен? Известен, посему делаем финт хвостом:</p>
<pre name="code" class="delphi">
uses ..., SHDocVw;

 
function OpenExplorerAndSelectFile(Path: String): Boolean;
 
  function ParceURLName(const Value: String): String;
  const
    scFilePath: array [0..7] of Char = ('f', 'i', 'l', 'e', ':', '/', '/', '/');
  begin
    if CompareMem(@scFilePath[0], @Value[1], 8) then
    begin
      Result := Copy(Value, 9, Length(Value));
      Result := StringReplace(Result, '/', '\', [rfReplaceAll]);
      Result := StringReplace(Result, '%20', ' ', [rfReplaceAll]);
      Result := IncludeTrailingBackslash(Result);
    end
    else
      Result := Value;
  end;
 
var
  iShellWindow: IShellWindows;
  iWB: IWebBrowserApp;
  spDisp: IDispatch;
  I: Integer;
  S, FilePath, FileName: String;
begin
  Result := FileExists(Path);
  if not Result then Exit;
  FilePath := AnsiUpperCase(ExtractFilePath(Path));
  FileName := ExtractFileName(Path);
  iShellWindow := CoShellWindows.Create;
  for I := 0 to iShellWindow.Count - 1 do
  begin
    spDisp := iShellWindow.Item(I);
    if spDisp = nil then Continue;
    spDisp.QueryInterface(IWebBrowserApp, iWB);
    if iWB &lt;&gt; nil then
    begin
      S := ParceURLName(iWB.LocationURL);
      if AnsiUpperCase(S) = FilePath then
      begin
        SendMessage(iWB.HWnd, WM_SYSCOMMAND, SC_CLOSE, 0);
        Break;
      end;
    end;
  end;
  ShellExecute(0, 'open', 'explorer.exe',
    PChar('/select, ' + FileName), PChar(FilePath), SW_SHOWNORMAL);
end;
 
procedure TForm18.Button4Click(Sender: TObject);
begin
  if not OpenExplorerAndSelectFile('c:\windows\notepad.exe') then
    ShowMessage('Файл не найден.');
end;
</pre>
<br>
Это проще чем искать по заголовку. <br>
<p>&nbsp;</p>
<p>Взято из <a href="http://forum.sources.ru" target="_blank">http://forum.sources.ru</a></p>
<p id="author">Автор: Rouse_</p>
</div>

<div id="footer">
<hr/>
<p>Материал из DRKB Library<br/>Разработка и поддержка &mdash; <a title="DRKB на GitHub" href="https://github.com/quadroid/drkb" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
