<html>
<head>
  <title>Таблица NetBIOS-имен удаленных компьютер</title>
  <meta http-equiv="Content-Type" content="text/html; charset=windows-1251" />
  <link type="text/css" href="css/css.css" rel="stylesheet" />
  <link type="text/css" href="css/sh.css" rel="stylesheet" />
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <div id="logo"><img id="logo" src="img/logo.png" /></div>
  <div id="navigation">
    <p id="navigation">
      <a class="navigation" onclick="prev();" href="#">Предыдущая</a><br />
      <a class="navigation" onclick="up();" href="#">Наверх</a><br />
      <a class="navigation" onclick="next();" href="#">Следующая</a>
    </p>
  </div>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Таблица NetBIOS-имен удаленных компьютер</h1>
<div id="date">01.01.2010</div>
<!-- Actual content start -->

<pre name="code" class="delphi">
////////////////////////////////////////////////////////////////////////////////
//
//  ****************************************************************************
//  * Unit Name : Unit1
//  * Purpose   : Демо получение информации NETBIOS именах компьютера
//                (аналог nbtstat -a)
//  * Author    : Александр (Rouse_) Багель
//  * Version   : 1.00
//  ****************************************************************************
//
 
unit Unit1;
 
interface
 
uses
  Windows, Messages, SysUtils, Classes, Graphics, Controls, Forms,
  Dialogs, Nb30, StdCtrls;
 
type
  TNetbiosInfo = record
    AdapterStatus: TAdapterStatus;
    NameBuffer: Array [0..30] of TNameBuffer;
  end;
 
  TForm1 = class(TForm)
    Edit1: TEdit;
    Button1: TButton;
    Memo1: TMemo;
    procedure Button1Click(Sender: TObject);
  private
    procedure GetNetBiosNames(const Addr: String);
  end;
 
var
  Form1: TForm1;
 
implementation
 
{$R *.dfm}
 
procedure TForm1.GetNetBiosNames(const Addr: String);
var
  NCB: TNCB;
  Enum: TLanaEnum;
  NetbiosInfo: TNetbiosInfo;
  I, A: Integer;
  Ends: Byte;
begin
  FillChar(NCB, SizeOf(TNCB), #0);
  NCB.ncb_command := Char(NCBENUM);
  NCB.ncb_buffer := @Enum;
  NCB.ncb_length := SizeOf(TLanaEnum);
  if Netbios(@NCB) = Char(NRC_GOODRET) then
    for I := 0 to Integer(Enum.length) - 1 do
    begin
      FillChar(NCB, SizeOf(TNCB), #0);
      NCB.ncb_lana_num := Enum.lana[I];
      NCB.ncb_command := Char(NCBRESET);
      if Netbios(@NCB) = Char(NRC_GOODRET) then
      begin
        FillChar(NetbiosInfo, SizeOf(TNetbiosInfo), #0);
        NCB.ncb_command := Char(NCBASTAT);
        Move(Addr[1], NCB.ncb_callname[0], Length(Addr));
        NCB.ncb_buffer := @NetbiosInfo;
        NCB.ncb_length := SizeOf(TNetbiosInfo);
        if Netbios(@NCB) = Char(NRC_GOODRET) then
          for A := 0 to NetbiosInfo.AdapterStatus.name_count - 1 do
          begin
            Ends := Byte(NetbiosInfo.NameBuffer[A].name[NCBNAMSZ - 1]);
            NetbiosInfo.NameBuffer[A].name[NCBNAMSZ - 1] := #32;
            Memo1.Lines.Add(Format('%s &lt;%s&gt;',
              [String(NetbiosInfo.NameBuffer[A].name),
                IntToHex(Ends, 2)]));
          end;
      end;
    end;
end;
 
procedure TForm1.Button1Click(Sender: TObject);
begin
  GetNetBiosNames(Edit1.Text);
end;
 
end.
</pre>
<p>&nbsp;<br>
&nbsp;<br>
<p>Взято из <a href="http://forum.sources.ru" target="_blank">http://forum.sources.ru</a></p>
<p id="author">Автор: Rouse_</p>
&nbsp;<br>

</div>
<!-- Actual content end -->
<hr />
<div id="footer">
<p>&copy; DRKB Library, 2010<br />Разработка и поддержка &mdash; <a href="http://www.drkb.ru/" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
