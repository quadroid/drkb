<!DOCTYPE html>
<html>
<head>
  <title>Разбор адреса FTP</title>
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8"/>
  <link type="text/css" href="css/css.css" rel="stylesheet"/>
  <link type="text/css" href="css/sh.css" rel="stylesheet"/>
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <a title="DRKB Library" href="../splash.html"><div id="logo"></div></a>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Разбор адреса FTP</h1>
<div id="date">01.01.2010</div>

<pre name="code" class="delphi">
{ **** UBPFD *********** by kladovka.net.ru ****
&gt;&gt; Разбор адреса FTP
 
Функция анализирует строку aSrc и если в стоке дан корректный адрес FTP, возвращает имя пользователя, пароль, хост, порт и каталог. Если дан некорректный адрес, функция возвращает false, иначе - true.
Учтена возможность, когда в адресе не указываются некоторые параметры. В этом случае, если нотация соблюдена, опущенные параметры возвращаются пустыми строками.
 
Минимально подробный адрес: ftp://myftp.ru - В этом случае будет возвращён адрес, порт по умолчанию (21) и true.
Максимально подробный адрес: ftp://MyLogin:MyPassword@MyFtp.ru:6000/MyDirectory/ - Будут возвращены все параметры и true.
 
Зависимости: Classes, SysUtils
Автор:       Роман Василенко, romix@nm.ru, ICQ:826361, Пятигорск
Copyright:   Роман Василенко
Дата:        13 июня 2004 г.
********************************************** }
 
function ParseFTP(aSrc: string; out oUserName, oPassword, oHost: string;
    out oPort: word; out oDir: string): boolean;
const
    ssPrefix = 0;
    ssUserName = 1;
    ssPassword = 2;
    ssHost = 3;
    ssPort = 4;
    ssDir = 5;
var
    f, StrState: integer;
    sPort, pfx: string;
begin
    oUserName:='';
    oPassword:='';
    oHost:='';
    sPort:='';
    oPort:=21;
    oDir:='';
    StrState:=ssPrefix;
    result:=false;
    f:=1;
    repeat
        case StrState of
            ssPrefix: //Разбор префикса ftp:// и определение анонимности адреса
            if aSrc[f]&lt;&gt;':' then
                pfx:=pfx+aSrc[f]
            else begin
                if (ansisametext(trim(pfx),'ftp')) and
                   (aSrc[f]+aSrc[f+1]+aSrc[f+2]='://') then begin
                    if pos('@',aSrc)&lt;1 then
                        StrState:=ssHost //Anonymous
                    else
                        StrState:=ssUserName; //UserName[:Password]
                    inc(f,2);
                end else break;
            end;
            ssUserName: //Извлечение имени пользователя
            if aSrc[f]='@' then //Пароль не указан
                StrState:=ssHost
            else
                if aSrc[f]=':' then //Пароль указан
                    StrState:=ssPassword
                else
                    oUserName:=oUserName+aSrc[f];
            ssPassword: //Извлечение пароля
            if aSrc[f]='@' then
                StrState:=ssHost
            else
                oPassword:=oPassword+aSrc[f];
            ssHost: //Извлечение хоста
            if aSrc[f]=':' then //Порт указан
                StrState:=ssPort
            else
                if aSrc[f]='/' then //Порт не указан, возможно указан каталог
                    StrState:=ssDir
                else
                    oHost:=oHost+aSrc[f];
            ssPort: //Извлечение порта
            if aSrc[f]='/' then
                StrState:=ssDir
            else
                sPort:=sPort+aSrc[f];
            ssDir: oDir:=oDir+aSrc[f];
        end;
        inc(f);
    until f&gt;length(aSrc);
    if (StrState&gt;ssPassword) and (trim(oHost)&lt;&gt;'') then
        result:=true;
    try
        if trim(sPort)&lt;&gt;'' then oPort:=strtoint(sPort);
    except
        result:=false;
    end;
end; 
</pre>

<p> Пример использования:</p>
<pre name="code" class="delphi">
...
 
procedure TForm1.Button1Click(Sender: TObject);
const
    yn: array[false..true] of string=('INVALID', 'VALID');
var
    xValid: boolean;
    xUser, xPassword, xHost, xDir: string;
    xPort: word;
begin
    xValid:=ParseFTP(Edit1.Text, xUser, xPassword, xHost, xPort, xDir);
    ShowMessage(format('Address is %s'#13#13'Host: %s'#13'Port: %d'#13+
        'Dir: %s'#13'User: %s'#13'Password: %s',
        [yn[xValid], xHost, xPort, xDir, xUser, xPassword]));
end;
 
... 
</pre>

</div>

<div id="footer">
<hr/>
<p>Материал из DRKB Library<br/>Разработка и поддержка &mdash; <a title="DRKB на GitHub" href="https://github.com/quadroid/drkb" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
