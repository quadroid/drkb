<!DOCTYPE html>
<html>
<head>
  <title>Копирование содержимого TStringGrid в буфер обмена</title>
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8"/>
  <link type="text/css" href="css/css.css" rel="stylesheet"/>
  <link type="text/css" href="css/sh.css" rel="stylesheet"/>
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <a title="DRKB Library" href="../splash.html"><div id="logo"></div></a>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Копирование содержимого TStringGrid в буфер обмена</h1>
<div id="date">01.01.2010</div>

<pre name="code" class="delphi">
{ **** UBPFD *********** by delphibase.endimus.com ****
&gt;&gt; Копирование содержимого StringGrid в буфер обмена
 
Копирует содержимое ячеек StringGrid в ClipBoard в формате, позволяющем
вставку, например, в Word или Excel. При CopySel=True копирует выделение,
иначе всю таблицу или указанный диапазон (CL- левый столбец и т.д.).
 
Зависимости: Grids
Автор:       Борис Новгородов (MBo), mbo@mail.ru, Новосибирск
Copyright:   MBo
Дата:        30 апреля 2002 г.
***************************************************** }
 
procedure SGCopyToCLP(SG: TStringGrid; CopySel: Boolean; CL: integer = -1;
  RT: integer = -1; CR: integer = -1; RB: integer = -1);
var
  i, j: Integer;
  s: string;
begin
  s := '';
  with SG do
  begin
    if CopySel then
    begin
      CL := Selection.Left;
      CR := Selection.Right;
      RT := Selection.Top;
      RB := Selection.Bottom;
    end;
    //при необходимости FixedRows и FixedCols можно заменить на 0
    if (CL &lt; FixedCols) or (CL &gt; CR) or (CL &gt;= ColCount) then
      CL := FixedCols;
    if (CR &lt; FixedCols) or (CL &gt; CR) or (CR &gt;= ColCount) then
      CR := ColCount - 1;
    if (RT &lt; FixedRows) or (RT &gt; RB) or (RT &gt;= RowCount) then
      RT := FixedRows;
    if (RB &lt; FixedCols) or (RT &gt; RB) or (RB &gt;= RowCount) then
      RB := RowCount - 1;
    for i := RT to RB do
    begin
      for j := CL to CR do
      begin
        s := s + Cells[j, i];
        if j &lt; CR then
          s := s + #9;
      end;
      s := s + #13#10;
  end;
  end;
  ClipBoard.AsText := s;
end;
 
// Пример использования:
SGCopyToCLP(StringGrid1, True); //выделение
SGCopyToCLP(StringGrid1, False); //все ячейки
SGCopyToCLP(StringGrid1, False, 1, 1, 3, 2); //диапазон, 6 ячеек
</pre>

</div>

<div id="footer">
<hr/>
<p>Материал из DRKB Library<br/>Разработка и поддержка &mdash; <a title="DRKB на GitHub" href="https://github.com/quadroid/drkb" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
