<!DOCTYPE html>
<html>
<head>
  <title>Создание Microsoft Office 2000 COM Add-Ins</title>
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8"/>
  <link type="text/css" href="css/css.css" rel="stylesheet"/>
  <link type="text/css" href="css/sh.css" rel="stylesheet"/>
  <script language="javascript" src="js/shInit.js"></script>
  <script language="javascript" src="js/shCore.js"></script>
  <script language="javascript" src="js/shBrushDelphi.js"></script>
  <script language="javascript" src="js/shBrushSql.js"></script>
</head>
<body>
<div id="layout">
<div id="header">
  <a title="DRKB Library" href="../splash.html"><div id="logo"></div></a>
</div>
<div id="content">
<div id="explorer">DRKB Explorer</div>
  <h1 id="title">Создание Microsoft Office 2000 COM Add-Ins</h1>
<div id="date">01.01.2010</div>
<p>Создание Microsoft Office 2000 COM Add-Ins </p>
&nbsp;</p>
Анатолий Тенцер </p>
&nbsp;</p>
<p>Vit (2006) - в DRKB публикуется с сокращениями.</p>
&nbsp;</p>
Модель модулей расширения Office 2000</p>
В Office 2000 Microsoft ввела единый API для написания подключаемых модулей, расширяющих функциональные возможности пакета. Теперь возможно написание таких модулей, интегрируемых с любым из приложений пакета. Разработанный набор интерфейсов упрощает создание расширений, позволяя программисту сконцентрироваться на функциональности вместо того, чтобы изучать особенности каждого из приложений.</p>
Новая модель подключаемых модулей получила название COM Add-Ins. Как следует из него, модуль расширения представляет собой COM-сервер, который специальным образом зарегистрирован в системе. Приложение пакета Office при загрузке загружает зарегистрированные расширения и вызывает их методы, позволяя установить обработчики на необходимые для их дальнейшего функционирования события. По завершении работы приложение Office извещает об этом модули расширения, позволяя последним корректно освободить ресурсы.</p>
Общение модулей расширения с загрузившим их приложением идет на основе объектной модели Microsoft Office.</p>
Использование COM Add-In позволяет в корне изменить подходы к интеграции приложений с Microsoft Office. Например, вместо того, чтобы вызывать Word для генерации отчетов, можно, наоборот, встроить в него меню со списком отчетов, а по выбору пользователем нужного &#8211; запросить данные из БД и сформировать по ним документ.</p>
Интерфейс IDTExtensibility2</p>
Ключевым моментом в написании COM Add-In является реализация им интерфейса IDTExtensibility2, определённого следующим образом:</p>
<div style="text-align: left; text-indent: 0px; padding: 0px 0px 0px 0px; margin: 5px 0px 5px 0px;"><pre name="code" class="delphi">type
  IDTExtensibility2 = interface(IDispatch)
 &nbsp;&nbsp; ['{B65AD801-ABAF-11D0-BB8B-00A0C90F2744}']
 &nbsp;&nbsp; procedure OnConnection(const HostApp: IDispatch; 
 &nbsp;&nbsp;&nbsp;&nbsp; ext_ConnectMode: Integer; const AddInInst: IDispatch;
 &nbsp;&nbsp;&nbsp;&nbsp; var custom: PSafeArray); safecall;
 &nbsp;&nbsp; procedure OnDisconnection(ext_DisconnectMode: Integer; 
 &nbsp;&nbsp;&nbsp;&nbsp; var custom: PSafeArray); safecall;
 &nbsp;&nbsp; procedure OnAddInsUpdate(var custom: PSafeArray); safecall;
 &nbsp;&nbsp; procedure OnStartupComplete(var custom: PSafeArray); safecall;
 &nbsp;&nbsp; procedure BeginShutdown(var custom: PSafeArray); safecall;
  end;
</pre>
После загрузки зарегистрированного в качестве расширения COM-сервера, Office запрашивает у него этот интерфейс, и, если он реализован, вызывает соответствующие методы, позволяя расширению реализовать свою функциональность.</p>
Рассмотрим методы IDTExtensibility2 подробнее:</p>
<div style="text-align: justify; text-indent: 0px; padding: 0px 0px 0px 0px; margin: 0px 0px 8px 0px;"><pre name="code" class="delphi">procedure OnConnection(
  const HostApp: IDispatch;&nbsp;&nbsp; 
  ext_ConnectMode: Integer;
  const AddInInst: IDispatch;
  var custom: PSafeArray
); safecall;
</pre>
Вызывается при загрузке Add-In. В этот момент можно произвести требуемую инициализацию, добавить или удалить необходимые интерфейсные элементы, установить обработчики событий и т.п.</p>
Параметры, передаваемые методу:</p>
<table cellspacing="0" cellpadding="2" border="0" style="border: none border-spacing:0px; border-collapse: collapse;">
<tr>
<td>HostApp</p>
</td>
<td>Ссылка на интерфейс IDispatch вызывающего приложения. Если Add-In планирует в дальнейшем обращаться к его объектной модели, он должен сохранить ссылку в переменной</p>
</td>
</tr>
<tr>
<td>ext_ConnectMode</p>
</td>
<td>Константа, информирующая о том, по какой причине производится загрузка:</p>
ext_cm_AfterStartup</p>
Add-In загружен после загрузки хотя бы одного документа Office</p>
ext_cm_External</p>
Add-In загружен другим компонентом</p>
ext_cm_Startup</p>
Add-In загружен до загрузки хотя бы одного документа Office</p>
ext_cm_CommandLine</p>
Не используется в Office 2000</p>
</td>
</tr>
<tr>
<td>AddinInst</p>
</td>
<td>Ссылка на интерфейс IDispatch загружаемого Add-In`а</p>
</td>
</tr>
<tr>
<td>Custom</p>
</td>
<td>Здесь и далее &#8211; в Microsoft Office не используется
</td>
</tr>
</table>
procedure OnDisconnection(ext_DisconnectMode: Integer; <br>
<p>  var custom: PSafeArray); safecall;</p>
Вызывается при выгрузке модуля расширения. Этот метод должен произвести освобождение занятых ресурсов и прочие процедуры по завершению работы COM-сервера.</p>
Параметр ext_DisconnectMode информирует о причине выгрузки COM-сервера и может принимать следующие значения:</p>
ext_dm_HostShutdown &#8211; приложение завершает работу, </p>
ext_dm_UserClosed &#8211; модуль расширения выгружен пользователем</p>
procedure OnAddInsUpdate(var custom: PSafeArray); safecall;</p>
Вызывается при изменении списка загруженных расширений (Add-Ins). COM-сервер может проанализировать коллекцию расширений приложения и предпринять необходимые действия, требующие взаимодействия с другими&nbsp; модулями расширений.</p>
procedure OnStartupComplete(var custom: PSafeArray); safecall;</p>
Вызывается по завершении инициализации приложения. Те модули расширений, которые должны предоставлять интерфейс пользователя при запуске приложения, должны делать это в методе OnStartupComplete, когда приложение полностью завершило этап инициализации.</p>
procedure BeginShutdown(var custom: PSafeArray); safecall;</p>
Вызывается в начале процесса завершения приложения, позволяя расширениям предпринять какие-либо действия в этот момент.</p>
Внедрение в объектную модель Office</p>
Как было рассмотрено в предыдущем разделе, расширению доступен интерфейс IDispatch вызывающего приложения. Таким образом, оно может модифицировать интерфейс этого приложения, добавляя и удаляя необходимые интерфейсные элементы, создавать документы, вызывать различные методы для работы с ними. Однако, как правило, также необходимо обеспечить реакцию на различные события в вызывающем приложении (такие, как изменение документа или нажатие кнопки на панели инструментов). Для этого Add-In должен установить обработчик соответствующего события. Прежде чем перейти к вопросам реализации, совершим небольшую экскурсию в события COM и их обработчики.</p>
COM Events</p>
Введение</p>
Если COM-объект хочет получать информацию о событиях в другом COM-объекте, то он должен уведомить об этом объект-источник событий, зарегистрировав себя в списке объектов-получателей уведомлений о событиях. Модель COM предоставляет для этого стандартный механизм.</p>
Объект-источник событий (в нашем случае &#8211; приложение Office, документ, кнопка на панели инструментов и т.п.) реализует интерфейс IConnectionPointContainer. Объект, нуждающийся в оповещении о событиях, должен запросить у источника этот интерфейс. Затем при помощи метода FindConnectionPoint получить «точку подключения» &#8211; интерфейс IConnectionPoint, и при помощи метода Advise зарегистрировать в этой точке подключения ссылку на свою реализацию интерфейса IDispatch, методы которого будут вызываться при возникновении тех или иных событий в источнике событий.</p>
Различные объекты Office определяют интерфейсы, которым должны соответствовать обработчики их событий. Так, например, для объекта CommandBarButton (кнопка на панели инструментов) определен интерфейс обработчика &#8211;</p>
type<br>
  _CommandBarButtonEvents = dispinterface<br>
 &nbsp;&nbsp; ['{000C0351-0000-0000-C000-000000000046}']<br>
 &nbsp;&nbsp; procedure Click(const Ctrl: CommandBarButton; <br>
 &nbsp;&nbsp;&nbsp;&nbsp; var CancelDefault: WordBool); dispid 1;<br>
<p>  end;</p>
Это означает, что при нажатии кнопки будет вызван метод Invoke зарегистрированного интерфейса-обработчика, с параметром DispId, равным 1. При этом параметр pDispIds будет содержать указатель на вариантный массив из двух параметров, соответственно, типов CommandBarButton и WordBool.</p>
Базовый класс обработчика COM-событий</p>
Реализуем базовый класс обработчика COM событий (реализация взята из модуля SinkObject.pas, написанного Binh Ly).</p>
<pre name="code" class="delphi">
type
  TBaseSink = class(TObject, IUnknown, IDispatch)
  protected
    { методы IUnknown }
    function QueryInterface(const IID: TGUID; out Obj): HResult;
      stdcall;
    function _AddRef: Integer; stdcall;
    function _Release: Integer; stdcall;
    { методы IDispatch }
    function GetIDsOfNames(const IID: TGUID; Names: Pointer;
      NameCount, LocaleID: Integer; DispIDs: Pointer): HResult;
      virtual; stdcall;
    function GetTypeInfo(Index, LocaleID: Integer; 
      out TypeInfo): HResult; virtual; stdcall;
    function GetTypeInfoCount(out Count: Integer): HResult; 
      virtual; stdcall;
    function Invoke(DispID: Integer; const IID: TGUID; 
      LocaleID: Integer; Flags: Word; var Params; 
      VarResult, ExcepInfo, ArgErr: Pointer): HResult; 
      virtual; stdcall;
private
    FCookie: Integer;
    FCP: IConnectionPoint;
    FSinkIID: TGuid;
    FSource: IUnknown;
    function DoInvoke(DispID: Integer; const IID: TGUID; 
      LocaleID: Integer;  Flags: Word; var dps: TDispParams; 
      pDispIds: PDispIdList;  VarResult, ExcepInfo, 
      ArgErr: Pointer): HResult; virtual; abstract;
  public
    destructor Destroy; override;
    procedure Connect(pSource : IUnknown);
    procedure Disconnect;
    property SinkIID: TGuid read FSinkIID;
    property Source: IUnknown read FSource;
  end;
</pre>
&nbsp;</p>
Рассмотрим реализацию ключевых методов этого класса. Метод QueryInterface, в дополнение к стандартной реализации проверяет, не пытаются ли запросить интерфейс обработчика событий. В этом случае возвращается IDispatch, позволяющий объекту-источнику событий вызвать метод Invoke</p>
<pre name="code" class="delphi">
function TBaseSink.QueryInterface(const IID: TGUID; 
  out Obj): HResult;
begin
  Result := E_NOINTERFACE;
  Pointer(Obj) := NIL;
  if GetInterface(IID, Obj) then
    Result := S_OK;
  // если запрашивается интерфейс SinkIID - 
  // возвращаем свой IDispatch
  if not Succeeded(Result) then
    if IsEqualIID(IID, FSinkIID) then
      if GetInterface(IDispatch, Obj) then
        Result := S_OK;
end;
</pre>
Метод Connect регистрирует COM-объект в качестве обработчика событий COM-объекта pSource. Обращаю внимание, что переменная FCP объявлена как поле класса, т.к. он должен удерживать счетчик ссылок на «точку подключения» пока она используется. Если объявить FCP как локальную переменную &#8211; по завершении метода Connect произойдет неявный вызов FCP._Release, что приведет к неправильной работе.</p>
<pre name="code" class="delphi">procedure TBaseSink.Connect(pSource: IUnknown);
var
  pcpc: IConnectionPointContainer;
begin
  Disconnect;
  try
 &nbsp;&nbsp; // Запрашиваем интерфейс IConnectionPointContainer у объекта-
 &nbsp;&nbsp; // источника событий
 &nbsp;&nbsp; OleCheck(pSource.QueryInterface(IConnectionPointContainer,
 &nbsp;&nbsp;&nbsp;&nbsp; pcpc));
 &nbsp;&nbsp; // Запрашиваем интерфейс IConnectionPoint.
 &nbsp;&nbsp; OleCheck(pcpc.FindConnectionPoint(FSinkIID, FCP));
 &nbsp;&nbsp; // Подключаемся к обработчику событий
 &nbsp;&nbsp; OleCheck(FCP.Advise(Self, FCookie));
 &nbsp;&nbsp; // Все прошло успешно - устанавливаем свойство Source
 &nbsp;&nbsp; FSource := pSource;
  except
 &nbsp;&nbsp; raise Exception.Create(Format('Unable to connect %s.'#13'%s',
 &nbsp;&nbsp;&nbsp;&nbsp; [ClassName, Exception(ExceptObject).Message]));
  end;
end;
</pre>
Метод Disconnect отключает обработчик событий от объекта-источника</p>
<pre name="code" class="delphi">procedure TBaseSink.Disconnect;
begin
  if FSource = NIL then
 &nbsp;&nbsp; Exit;
  try
 &nbsp;&nbsp; OleCheck(FCP.Unadvise(FCookie));
 &nbsp;&nbsp; FCP := NIL;
 &nbsp;&nbsp; FSource := NIL;
  except
 &nbsp;&nbsp; Pointer(FCP) := NIL;
 &nbsp;&nbsp; Pointer(FSource) := NIL;
  end;
end;
</pre>
Метод Invoke вызывается при возникновении события в объекте источнике. Он осуществляет предварительную обработку параметров и вызывает абстрактный метод DoInvoke, который должен быть перекрыт в наследниках, реализующих конкретные интерфейсы обработчиков событий. Реализация такого наследника будет рассмотрена ниже.</p>
Обработчик событий от CommandBarButton</p>
Наследуя функциональность от базового класса TBaseSink, обработчики событий конкретных COM-объектов реализуются перекрытием методов Create и DoInvoke. Создадим такой обработчик для кнопки на панели инструментов Office. Он должен реализовать интерфейс _CommandBarButtonEvents &#8211; </p>
<pre name="code" class="delphi">type
  _CommandBarButtonEvents = dispinterface
 &nbsp;&nbsp; ['{000C0351-0000-0000-C000-000000000046}']
 &nbsp;&nbsp; procedure Click(const Ctrl: CommandBarButton; 
 &nbsp;&nbsp;&nbsp;&nbsp; var CancelDefault: WordBool); dispid 1;
  end;
</pre>
Объявим класс:</p>
<pre name="code" class="delphi">type
  // Обработчик события нажатия на кнопку
  TOnCommandButtonClick = procedure (Button: CommandBarButton;
 &nbsp;&nbsp;&nbsp; var CancelDefault: WordBool) of object;
TCommandButtonEventSink = class(TBaseSink)
  private
 &nbsp;&nbsp; FOnClick: TOnCommandButtonClick;
  protected
 &nbsp;&nbsp; procedure DoClick(Button: CommandBarButton;
 &nbsp;&nbsp;&nbsp;&nbsp; var CancelDefault: WordBool); virtual;
 &nbsp;&nbsp; function DoInvoke (DispID: Integer; const IID: TGUID; 
 &nbsp;&nbsp;&nbsp;&nbsp; LocaleID: Integer; Flags: Word; var dps : TDispParams; 
 &nbsp;&nbsp;&nbsp;&nbsp; pDispIds : PDispIdList; VarResult, ExcepInfo, 
 &nbsp;&nbsp;&nbsp;&nbsp; ArgErr: Pointer): HResult; override;
  public
 &nbsp;&nbsp; constructor Create; virtual;
 &nbsp;&nbsp; property OnClick: TOnCommandButtonClick 
 &nbsp;&nbsp;&nbsp;&nbsp; read FOnClick write FOnClick;
  end;
</pre>
В конструкторе установим идентификатор интерфейса обработчика событий, который мы реализуем.</p>
<pre name="code" class="delphi">constructor TCommandButtonEventSink.Create;
begin
  inherited;
  FSinkIID := _CommandBarButtonEvents;
end;
</pre>
Метод DoClick просто вызывает назначенный классу обработчик события, и нужен для более удобной работы с ним из Delphi</p>
<pre name="code" class="delphi">
procedure TCommandButtonEventSink.DoClick(Button: CommandBarButton;
  var CancelDefault: WordBool);
begin
  if Assigned(FOnClick) then
 &nbsp;&nbsp; FOnClick(Button, CancelDefault);
end;
</pre>
Ключевым методом является DoInvoke, который для каждого DispId, объявленного в интерфейсе _CommandBarButtonEvents, должен выполнить соответствующие действия</p>
<pre name="code" class="delphi">function TCommandButtonEventSink.DoInvoke(DispID: Integer;
  const IID: TGUID; LocaleID: Integer; Flags: Word; 
  var dps: TDispParams; pDispIds: PDispIdList; 
  VarResult, ExcepInfo, ArgErr: Pointer): HResult;
begin
  Result := S_OK;
  case DispID of
 &nbsp;&nbsp; // Для этого DispId передаются 2 параметра – CommandBarButton
 &nbsp;&nbsp; // и WordBool
 &nbsp;&nbsp; 1 : DoClick(IUnknown(dps.rgvarg^[pDispIds^[0]].unkval) 
 &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; as CommandBarButton, dps.rgvarg^[pDispIds^[1]].pbool^);
  else
 &nbsp;&nbsp; Result := DISP_E_MEMBERNOTFOUND;
  end;
end;
</pre>
Как видим, реализация конкретного обработчика является практически механической задачей, и не должна вызвать проблем. Если в интерфейсе предусмотрено несколько методов, то надо подставить в оператор case все их DispId`ы. От программиста требуется лишь аккуратность при отображении массива dps на параметры соответствующих обработчиков.</p>
Регистрация модулей</p>
COM Add-In &#8211; это COM-сервер, который должен быть зарегистрирован в системе, например, при помощи TRegSvr.exe или RegSvr32.exe. Однако требуется еще один шаг &#8211; регистрация его в Microsoft Office. Для этого необходимо создать в реестре раздел с именем</p>
&nbsp;</p>
HKEY_CURRENT_USER\Software\Microsoft\Office\&lt;Имя приложения&gt;\AddIns\&lt;Имя&gt;</p>
&nbsp;</p>
Здесь:</p>
&lt;Имя приложения&gt; - название приложения, к которому подключается Add-In</p>
&lt;Имя&gt; - имя, под которым зарегистрирован COM-сервер (название_проекта.имя_класса).</p>
&nbsp;</p>
В этом разделе необходимо создать два параметра:</p>
<table cellspacing="0" cellpadding="4" border="0" style="border: none border-spacing:0px; border-collapse: collapse;">
<tr>
<td>FriendlyName</p>
</td>
<td>Строковый параметр, определяющий имя, под которым наше расширение будет видно в менеджере расширений приложений Microsoft Office</p>
</td>
</tr>
<tr>
<td>LoadBehavior</p>
</td>
<td>Параметр типа DWORD, определяющий, когда должен загружаться Add-In
</td>
</tr>
</table>
Параметр LoadBehavior может принимать одно из следующих значений:</p>
<table cellspacing="0" cellpadding="4" border="0" style="border: none border-spacing:0px; border-collapse: collapse;">
<tr>
<td>3</p>
</td>
<td>Add-In загружается при старте приложения</p>
</td>
</tr>
<tr>
<td>9</p>
</td>
<td>Add-In загружается по требованию (когда его свойство Connected в колекции AddIns приложения будет установлено в TRUE)</p>
</td>
</tr>
<tr>
<td>16</p>
</td>
<td>Add-In загружается один раз при следующем запуске приложения. 
</td>
</tr>
</table>
Пишем COM Add-In</p>
Библиотеки типов Office 2000</p>
Для работы с объектной моделью Microsoft Office 2000 нам понадобятся библиотеки типов, описывающие доступные интерфейсы. Так как Delphi 5 поставляется с библиотеками типов от Office 97, необходимо импортировать нужные модули. В любом случае понадобится библиотека Office_TLB, остальные (Word_TLB,Excel_TLB и т.п.) понадобятся в зависимости от того, к какому приложению будет писаться модуль расширения.</p>
<img src="pic/clip0242.gif" width="477" height="579" border="0" alt="clip0242"></p>
Для импорта воспользуемся меню Import TypeLibrary.</p>
После создания модулей с описаниями интерфейсов рекомендую вручную удалить из них ссылки на модули Graphics и OleCtrls, которые приводят к подключению к проекту модуля Forms, а при компиляции не нужны.</p>
Создаем COM-сервер</p>
Поскольку COM Add-In является COM-сервером, то воспользуемся мастерами Delphi для его создания. Выберем команду New... -&gt; ActiveX -&gt; ActiveX Library</p>
Затем добавим в созданную библиотеку Automation Object, при помощи мастера New... -&gt; ActiveX -&gt; Automation Object </p>
&nbsp;</p>
В поле CoClassName введем имя реализуемого интерфейса (DTExtensibility2). В принципе можно ввести любое имя, необходимо только, чтобы этот интерфейс имел тот же GUID, что и IDTExtensibility и аналогичный набор методов.</p>
После того, как Delphi создаст новый объект автоматизации, запустим редактор библиотеки типов. В начале мы имеем описание интерфейса без методов и со сгенерированным Delphi значением GUID</p>
&nbsp;</p>
Перейдем на вкладку Text созданного интерфейса и введем там следующий текст:</p>
&nbsp;</p>
[<br>
  uuid(B65AD801-ABAF-11D0-BB8B-00A0C90F2744), <br>
  version(1.0), <br>
  helpstring("Dispatch interface for Office2000ComAddIn Object"), <br>
  dual, <br>
  oleautomation<br>
<p>]</p>
interface IDTExtensibility2: IDispatch<br>
{<br>
  [id(0x00000001)]<br>
  HRESULT _stdcall OnConnection(<br>
 &nbsp;&nbsp; [in] IDispatch * HostApp, <br>
 &nbsp;&nbsp; [in] long ext_ConnectMode, <br>
 &nbsp;&nbsp; [in] IDispatch * AddInInst, <br>
 &nbsp;&nbsp; [in] SAFEARRAY(VARIANT) * custom );<br>
  [id(0x00000002)]<br>
  HRESULT _stdcall OnDisconnection([in] long ext_DisconnectMode, <br>
 &nbsp;&nbsp; [in] SAFEARRAY(VARIANT) * custom );<br>
  [id(0x00000003)]<br>
  HRESULT _stdcall OnAddInsUpdate(<br>
 &nbsp;&nbsp; [in] SAFEARRAY(VARIANT) * custom );<br>
  [id(0x00000004)]<br>
  HRESULT _stdcall OnStartupComplete(<br>
 &nbsp;&nbsp; [in] SAFEARRAY(VARIANT) * custom );<br>
  [id(0x00000005)]<br>
  HRESULT _stdcall BeginShutdown(<br>
 &nbsp;&nbsp; [in] SAFEARRAY(VARIANT) * custom );<br>
<p>};</p>
&nbsp;</p>
Если Вы назвали свой класс не DTExtensibility2, а как-то по другому &#8211; скорректируйте название интерфейса. Все остальное, включая uuid должно быть введено именно так, как это было описано выше.</p>
Если все введено правильно &#8211; в окне Type Library Editor Вы должны увидеть пять методов созданного интерфейса</p>
&nbsp;</p>
Нажмите кнопку «обновить» и закройте редактор библиотеки типов, больше он нам не понадобится. Теперь откомпилируйте полученный проект и зарегистрируйте его в Windows при помощи меню Run-&gt;Register COM Server.</p>
При помощи редактора реестра создайте в реестре Windows запись для регистрации Add-In с приложением Microsoft Office. COM Add-In готов! </p>
Отладка Add-Ins</p>
Для работы с COM Add-Ins необходимо добавить в меню Microsoft Office команду для вызова диспетчера дополнений. Для этого (на примере Word)</p>
<div style="text-align: justify; text-indent: 0px; padding: 0px 0px 0px 0px; margin: 0px 0px 8px 24px;"><table border="0" cellpadding="0" cellspacing="0" style="line-height: normal;"><tr><td width="24">1.</td><td>В меню Сервис выберите команду Настройка, а затем &#8212; вкладку Команды.</td></tr></table></div><div style="text-align: justify; text-indent: 0px; padding: 0px 0px 0px 0px; margin: 0px 0px 8px 24px;"><table border="0" cellpadding="0" cellspacing="0" style="line-height: normal;"><tr><td width="24">2.</td><td>В списке Категории выберите категорию Сервис.</td></tr></table></div><div style="text-align: justify; text-indent: 0px; padding: 0px 0px 0px 0px; margin: 0px 0px 8px 24px;"><table border="0" cellpadding="0" cellspacing="0" style="line-height: normal;"><tr><td width="24">3.</td><td>Перетащите команду Надстройки для модели COM... из списка Команды в меню Сервис. Когда в меню Сервис раскроется список команд, укажите, где в этом меню должна располагаться команда Надстройки для модели COM..., после чего отпустите кнопку мыши.</td></tr></table></div><div style="text-align: justify; text-indent: 0px; padding: 0px 0px 0px 0px; margin: 0px 0px 8px 24px;"><table border="0" cellpadding="0" cellspacing="0" style="line-height: normal;"><tr><td width="24">4.</td><td>Нажмите кнопку "Закрыть".</td></tr></table></div>При помощи этой команды вызывается окно диспетчера дополнений. Загруженные дополнения помечены в списке галочкой. Чтобы загрузить надстройку &#8211;отметьте её и нажмите OK, чтобы выгрузить &#8211; снимите отметку.</p>
! &nbsp; &nbsp; &nbsp; &nbsp;Если во время загрузки Add-In произошла ошибка &#8211; Office не будет загружать его при следующем запуске приложения автоматически. Чтобы надстройка снова начала загружаться &#8211; загрузите её через диспетчер.</p>
Для отладки Com Add-Ins установите в качестве Host Application (меню Run - &gt; Parameters) приложение Office, к которому подключена надстройка, например WinWord.exe. После этого установите точку прерывания в одном из методов своего объекта и запустите приложение. Загрузится Word, и, при попадании на точку прерывания вы окажетесь в отладчике Delphi.</p>
Реализуем функциональность</p>
В настоящий момент наш Add In умеет только загружаться, но не может сделать ничего полезного. Для примера реализации функциональности добавим на панель инструментов Office кнопку, по нажатию которой в текущую позицию курсора вставляется список файлов выбранной папки.</p>
Для этого дополним наш объект автоматизации несколькими полями и методами</p>
<pre name="code" class="delphi">type
  TDirectoryList = class(TAutoObject, IDTExtensibility2)
  private
 &nbsp;&nbsp; Host: WordApplication;
 &nbsp;&nbsp; FButtonEventsSink: TCommandButtonEventSink;
 &nbsp;&nbsp; procedure ButtonClick(Button: CommandBarButton;
 &nbsp;&nbsp;&nbsp;&nbsp; var CancelDefault: WordBool);
  protected
 &nbsp;&nbsp; // Реализация IDTExtensibility2
 &nbsp;&nbsp; procedure BeginShutdown(var custom: PSafeArray); safecall;
 &nbsp;&nbsp; procedure OnAddInsUpdate(var custom: PSafeArray); safecall;
 &nbsp;&nbsp; procedure OnConnection(const HostApp: IDispatch; 
 &nbsp;&nbsp;&nbsp;&nbsp; ext_ConnectMode: Integer; const AddInInst: IDispatch;
 &nbsp;&nbsp;&nbsp;&nbsp; var custom: PSafeArray); safecall;
 &nbsp;&nbsp; procedure OnDisconnection(ext_DisconnectMode: Integer;
 &nbsp;&nbsp;&nbsp;&nbsp; var custom: PSafeArray); safecall;
 &nbsp;&nbsp; procedure OnStartupComplete(var custom: PSafeArray); safecall;
  end;
</pre>
&nbsp;</p>
Поле Host будет хранить ссылку на интерфейс WordApplication, необходимый для работы с объектной моделью Word, поле FButtonEventsSink &#8211; ссылку на объект-обработчик событий от кнопки, реализация которого была рассмотрена в разделе «Обработчик событий от CommandBarButton», а метод ButtonClick будет вызываться для обработки нажатия на кнопку.</p>
Реализуем необходимую функциональность в методах класса TDirectoryList</p>
<pre name="code" class="delphi">
const
  // Уникальный идентификатор кнопки. Можно задать любую уникальную
  // строку. Для её генерации удобно воспользоваться средствами 
  // Delphi по генерации GUID (Ctrl+Shift+G)
  // Этот идентификатор понадобится нам для того, чтобы после 
  // создания кнопки иметь возможность её найти.
  BUTTON_TAG = '{1A1552DC-9286-11D3-BCA0-00902759A497}';
&nbsp;
procedure TDirectoryList.OnConnection(const HostApp: IDispatch;
  ext_ConnectMode: Integer; const AddInInst: IDispatch;
  var custom: PSafeArray);
var
  Bar: CommandBar;
  Button: CommandBarButton;
begin
  // Сохраняем ссылку на WordApplication для
  // последующей работы с ней
  Host := HostApp as WordApplication;
  // Создаем обработчик событий для кнопки
  FButtonEventsSink := TCommandButtonEventSink.Create;
  FButtonEventsSink.OnClick := ButtonClick;
  // Получаем интерфейс панели инструментов "Форматирование"
  Bar := Host.CommandBars.Get_Item('Formatting');
  // Проверяем наличие на ней нашей кнопки
  Button := Bar.FindControl(msoControlButton, EmptyParam, BUTTON_TAG,
 &nbsp;&nbsp; EmptyParam, msoFalse) as CommandBarButton;;
  if not Assigned(Button) then
 &nbsp;&nbsp; // Если её нет – создаем
 &nbsp;&nbsp; Button := Bar.Controls.Add(msoControlButton, EmptyParam, 
 &nbsp;&nbsp;&nbsp;&nbsp; BUTTON_TAG, 1, EmptyParam) as CommandBarButton;
  // Подключаем обработчик и устанавливаем свойства кнопки
  FButtonEventsSink.Connect(Button);
  Button.Set_Style(msoButtonCaption);
  Button.Set_Tag(BUTTON_TAG);
  Button.Set_Caption('Dir');
end;
procedure TDirectoryList.OnDisconnection(ext_DisconnectMode: Integer;
  var custom: PSafeArray);
var
  Bar: CommandBar;
  B: CommandBarControl;
begin
  // Уничтожаем обработчик событий кнопки
  FreeAndNil(FButtonEventsSink);
  // Ищем свою кнопку
  Bar := Host.CommandBars.Get_Item('Formatting');
  B := Bar.FindControl(msoControlButton, EmptyParam, BUTTON_TAG,
 &nbsp;&nbsp; EmptyParam, msoFalse) as CommandBarButton;
  // И удаляем её
  if Assigned(B) then
 &nbsp;&nbsp; B.Delete(msoFalse);
end;
procedure TDirectoryList.ButtonClick(Button: CommandBarButton;
  var CancelDefault: WordBool);
var
  S: String;
  SR: TSearchRec;
  D: WordDocument;
  FindStatus: Integer;
begin
  // Эта процедура вызывается при нажатии на созданную нами кнопку
  D := Host.ActiveDocument;
  // Проверяем наличие активного документа
  if Assigned(D) then begin
 &nbsp;&nbsp; // Функция BrowseForFolder возвращает путь к выбраной
 &nbsp;&nbsp; // в диалоге папке. Код функции приведен в модуле с примером
 &nbsp;&nbsp; // на сопровождающем диска. От использования аналогичной
 &nbsp;&nbsp; // функции VCL SelectDirectory, пришлось отказаться, т.к. 
 &nbsp;&nbsp; // модуль в котором она находится использует Forms
 &nbsp;&nbsp; if BrowseForFolder(S) then begin
 &nbsp;&nbsp;&nbsp;&nbsp; with TStringList.Create do
 &nbsp;&nbsp;&nbsp;&nbsp; try
 &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; // Получаем список файлов
 &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; FindStatus := FindFirst(S + '\*.*', 0, SR);
 &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; while FindStatus = 0 do begin
 &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; Add(SR.Name);
 &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; FindStatus := FindNext(SR);
 &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; end;
 &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; FindClose(SR);
 &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; // И вставляем его в документ
 &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; D.ActiveWindow.Selection.InsertAfter(S+#13#13+Text);
 &nbsp;&nbsp;&nbsp;&nbsp; finally
 &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; // Освобождаем TStringList
 &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; Free;
 &nbsp;&nbsp;&nbsp;&nbsp; end;
 &nbsp;&nbsp; end;
  end;
end;
</pre>
Написание надстроек, работающих с несколькими приложениями Office</p>
Поскольку все приложения Office реализуют одну и ту же модель COM Add-Ins, то одно и то же расширение может быть зарегистрировано одновременно для нескольких приложений. В этом случае оно должно определять, из какого приложения оно загружено, и использовать соответствующую объектную модель. Определить приложение-владельца можно, запросив у него соответствующий интерфейс:</p>
<pre name="code" class="delphi">procedure TDirectoryList.OnConnection(const HostApp: IDispatch;
  ext_ConnectMode: Integer; const AddInInst: IDispatch;
  var custom: PSafeArray);
begin
  if HostApp is WordApplication then
  // Это Microsoft Word
</pre>
&nbsp;</p>
Также возможно сохранение HostApp в переменной типа Variant, и использование позднего связывания. В этом случае надстройка будет работать с любым приложением Office, имеющим подходящие по именам методы.</p>
</div>

<div id="footer">
<hr/>
<p>Материал из DRKB Library<br/>Разработка и поддержка &mdash; <a title="DRKB на GitHub" href="https://github.com/quadroid/drkb" target="_blank">Quadr0</a></p>
</div>
</div>
</body>
</html>
